# Choose an EESSI CVMFS repository
if (! $?EESSI_CVMFS_REPO) then
    set EESSI_CVMFS_REPO = "/cvmfs/software.eessi.io"
endif
# Choose an EESSI version
setenv EESSI_VERSION_DEFAULT "__EESSI_VERSION_DEFAULT__"
if (! $?EESSI_VERSION) then
    set EESSI_VERSION = "${EESSI_VERSION_DEFAULT}"
endif
# Now that we know our EESSI version let's not forget it
setenv EESSI_VERSION_DEFAULT "$EESSI_VERSION"

# ability to predefine elsewhere the default list (with options to append or prepend)
set LMOD_SYSTEM_DEFAULT_MODULES="EESSI/$EESSI_VERSION"
if ( $?EESSI_SYSTEM_DEFAULT_MODULES_PREPEND ) then
    set LMOD_SYSTEM_DEFAULT_MODULES="$EESSI_SYSTEM_DEFAULT_MODULES_PREPEND:$LMOD_SYSTEM_DEFAULT_MODULES"
endif
if ( $?EESSI_SYSTEM_DEFAULT_MODULES_APPEND ) then
    set LMOD_SYSTEM_DEFAULT_MODULES="$LMOD_SYSTEM_DEFAULT_MODULES:$EESSI_SYSTEM_DEFAULT_MODULES_APPEND"
endif
setenv LMOD_SYSTEM_DEFAULT_MODULES "$LMOD_SYSTEM_DEFAULT_MODULES"

if (! $?__Init_EESSI_Default_Modules ) then
    setenv __Init_EESSI_Default_Modules 1

    # Lmod version in 2023.06 has a problem with newer Lmod caches, so let's stick to more recent Lmod
    # (has no effect except on Lmod itself, and compatible caches are still created/supported by EESSI)
    set LMOD_EESSI_VERSION="$EESSI_VERSION:s/2023.06/2025.06/"

    # If there is a local Lmod, make it forget about the system set MODULEPATH
    unsetenv __LMOD_REF_COUNT_MODULEPATH
    # and clear out any memory Lmod might have
    unsetenv _ModuleTable001_
    # Path to top-level module tree
    set modulepath="$EESSI_CVMFS_REPO/init/modules"
    if ( $?EESSI_EXTRA_MODULEPATH && "$EESSI_EXTRA_MODULEPATH" != "" ) then
        set modulepath="$modulepath:$EESSI_EXTRA_MODULEPATH"
    endif
    setenv MODULEPATH "$modulepath"
    source "${EESSI_CVMFS_REPO}/versions/${LMOD_EESSI_VERSION}/compat/linux/`uname -m`/usr/share/Lmod/init/csh"

    module --initial_load --no_redirect restore
else
    module reset
endif
