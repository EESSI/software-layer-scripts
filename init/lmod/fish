#!/usr/bin/env fish

# SPDX-License-Identifier: GPL-2.0-only
# Copyright (C) 2020-2026 EESSI contributors
#
# EESSI - European Environment for Scientific Software Installations
#
# This file is a template for initialising EESSI via Lmod for the environment indicated by the shebang.
#
# Please refer to the reference bash implementation for full documentation, only minimal comments are included here

# Choose an EESSI CVMFS repository
set EESSI_CVMFS_REPO (set -q EESSI_CVMFS_REPO; and echo "$EESSI_CVMFS_REPO"; or echo "/cvmfs/software.eessi.io")

# Choose an EESSI version
if not set -q __EESSI_VERSION_USED_FOR_INIT
    set EESSI_VERSION_DEFAULT "__EESSI_VERSION_DEFAULT__"
else
    set EESSI_VERSION_DEFAULT "$__EESSI_VERSION_USED_FOR_INIT"
end
if not set -q EESSI_VERSION
    set EESSI_VERSION "$EESSI_VERSION_DEFAULT"
end

# Record version used for init; -x exports it to the environment
if not set -q __EESSI_VERSION_USED_FOR_INIT
    set -x __EESSI_VERSION_USED_FOR_INIT "$EESSI_VERSION"
end

# ability to predefine elsewhere the default list (with options to append or prepend)
set LMOD_SYSTEM_DEFAULT_MODULES "EESSI/$EESSI_VERSION"
if set -q EESSI_DEFAULT_MODULES_PREPEND
    set LMOD_SYSTEM_DEFAULT_MODULES "$EESSI_DEFAULT_MODULES_PREPEND:$LMOD_SYSTEM_DEFAULT_MODULES"
end
if set -q EESSI_DEFAULT_MODULES_APPEND
    set LMOD_SYSTEM_DEFAULT_MODULES "$LMOD_SYSTEM_DEFAULT_MODULES:$EESSI_DEFAULT_MODULES_APPEND"
end
set -x LMOD_SYSTEM_DEFAULT_MODULES $LMOD_SYSTEM_DEFAULT_MODULES

if test -z "$__Init_EESSI_Default_Modules"
    set -x __Init_EESSI_Default_Modules 1

    # Lmod version in 2023.06 has a problem with newer Lmod caches, so let's stick to more recent Lmod
    # (has no effect except on Lmod itself, and compatible caches are still created/supported by EESSI)
    set LMOD_EESSI_VERSION (string replace 2023.06 2025.06 -- $EESSI_VERSION)

    # If there is a local Lmod, make it forget about the system set MODULEPATH
    set -e __LMOD_REF_COUNT_MODULEPATH
    # and clear out any memory Lmod might have
    set -e _ModuleTable001_
    # Path to top-level module tree
    set modulepath "$EESSI_CVMFS_REPO/init/modules"
    if set -q EESSI_EXTRA_MODULEPATH; and test -n "$EESSI_EXTRA_MODULEPATH"
        set modulepath "$modulepath:$EESSI_EXTRA_MODULEPATH"
    end
    set -x MODULEPATH $modulepath
    . "$EESSI_CVMFS_REPO"/versions/"$LMOD_EESSI_VERSION"/compat/linux/(uname -m)/usr/share/Lmod/init/fish

    module --initial_load --no_redirect restore

    echo "EESSI has selected $EESSI_SOFTWARE_SUBDIR as the compatible CPU target for EESSI/$EESSI_VERSION"
    if test -n "$EESSI_ACCEL_SUBDIR"
        echo "EESSI has selected $EESSI_ACCEL_SUBDIR as the compatible accelerator target for EESSI/$EESSI_VERSION"
    else
        echo "EESSI did not identify an accelerator on the system"
    end
    echo "(for debug information when loading the EESSI module, set the environment variable EESSI_MODULE_DEBUG to 1)"
else
    module reset
end
