# Choose an EESSI CVMFS repository
set EESSI_CVMFS_REPO (set -q EESSI_CVMFS_REPO; and echo "$EESSI_CVMFS_REPO"; or echo "/cvmfs/software.eessi.io")
# Choose an EESSI version
set EESSI_VERSION_DEFAULT "__EESSI_VERSION_DEFAULT__"
set EESSI_VERSION (set -q EESSI_VERSION; and echo "$EESSI_VERSION"; or echo "$EESSI_VERSION_DEFAULT")
# Now that we know our EESSI version let's not forget it
set -x EESSI_VERSION_DEFAULT "$EESSI_VERSION"

# ability to predefine elsewhere the default list (with options to append or prepend)
set LMOD_SYSTEM_DEFAULT_MODULES "EESSI/$EESSI_VERSION"
if set -q EESSI_SYSTEM_DEFAULT_MODULES_PREPEND
    set LMOD_SYSTEM_DEFAULT_MODULES "$EESSI_SYSTEM_DEFAULT_MODULES_PREPEND:$LMOD_SYSTEM_DEFAULT_MODULES"
end
if set -q EESSI_SYSTEM_DEFAULT_MODULES_APPEND
    set LMOD_SYSTEM_DEFAULT_MODULES "$LMOD_SYSTEM_DEFAULT_MODULES:$EESSI_SYSTEM_DEFAULT_MODULES_APPEND"
end
set -x LMOD_SYSTEM_DEFAULT_MODULES $LMOD_SYSTEM_DEFAULT_MODULES

if test -z "$__Init_EESSI_Default_Modules"
    set -x __Init_EESSI_Default_Modules 1

    # Lmod version in 2023.06 has a problem with newer Lmod caches, so let's stick to more recent Lmod
    # (has no effect except on Lmod itself, and compatible caches are still created/supported by EESSI)
    set LMOD_EESSI_VERSION (string replace 2023.06 2025.06 -- $EESSI_VERSION)

    # If there is a local Lmod, make it forget about the system set MODULEPATH
    set -e __LMOD_REF_COUNT_MODULEPATH
    # and clear out any memory Lmod might have
    set -e _ModuleTable001_
    # Path to top-level module tree
    set modulepath "$EESSI_CVMFS_REPO/init/modules"
    if set -q EESSI_EXTRA_MODULEPATH; and test -n "$EESSI_EXTRA_MODULEPATH"
        set modulepath "$modulepath:$EESSI_EXTRA_MODULEPATH"
    end
    set -x MODULEPATH $modulepath
    . "$EESSI_CVMFS_REPO"/versions/"$LMOD_EESSI_VERSION"/compat/linux/(uname -m)/usr/share/Lmod/init/fish

    module --initial_load --no_redirect restore
else
    module reset
end
